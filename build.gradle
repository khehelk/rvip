plugins {
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

group = 'ru.khehelk.rviplabs'
version = '0.0.1-SNAPSHOT'

subprojects {
    def libraries = ['common']

    apply plugin: 'io.spring.dependency-management'

    if (libraries.contains(project.name)) {
        apply plugin: 'java-library'
        java {
            withSourcesJar()
        }
    } else {
        apply plugin: 'java'
        apply plugin: 'org.springframework.boot'
    }

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(21))
        }
    }

    compileJava.options.encoding = 'UTF-8'

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    ext {
        set('spring_boot_version', '3.3.3')
        set('spring_cloud_version', '2023.0.3')
        set('mapstruct_version', '1.6.0')
        set('springdoc_version', '2.6.0')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${spring_boot_version}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}